[script]
n0=/* 
n1=Slots
n2=
n3=A slot machine you can run from your Twitch chat!
n4=
n5=- created by Techaholica http://techaholica.com
n6=*/
n7=
n8=
n9=/*
n10=*****************************
n11=EVENTS
n12=***************************
n13=*/
n14=on *:JOIN:#: {
n15=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n16=
n17=  if (!$exists(%userFile)) {
n18=    // if the user doesn't already have a profile create one
n19=    createUserFile $nick
n20=  }
n21=
n22=  startUserTimer
n23=}
n24=
n25=on *:LOAD: {
n26=  setVariables
n27=}
n28=
n29=on *:PART:#:{
n30=  timer $+ $nick off
n31=}
n32=
n33=/*
n34=**************
n35=User commands
n36=**************
n37=*/
n38=on *:TEXT:!*:#: { 
n39=
n40=  // almost all commands need the user file so just set that up here
n41=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n42=
n43=  if ($1 == !dance) {
n44=    msg $chan $nick does a happy dance
n45=  } 
n46=  elseif ($1 == !danceParty) {
n47=    if ($nick isop $chan) {
n48=      msg $chan $nick starts a dance party. Everyone dance!!!!
n49=    }  
n50=    else {
n51=      msg $chan $nick Sneaky! Sneaky! Only admins can start a dance party.
n52=    }
n53=  } 
n54=  elseif ($1 == !addStats) {
n55=    if ($nick isop $chan) {
n56=      if (($2 == $null) || ($3 == $null) || ($4 == $null)) {
n57=        msg $chan $nick Usage: !addStats user property value
n58=      } 
n59=      else {
n60=        addStats $2 $3 $4
n61=      }
n62=    } 
n63=    else {
n64=      msg $chan $nick Sneaky! Sneaky! You must be an admin to use this command.
n65=    }
n66=  } 
n67=  elseif ($1 == !bet) {
n68=    if (($2 != $null) && ($2 isnum)) {
n69=      if ($2 > 0) {
n70=        var %bet $round($2, 2)
n71=
n72=        writeini %userFile character bet %bet 
n73=        msg $chan $nick You are now betting %bet for all gambling games
n74=      }
n75=      else {
n76=        msg $chan $nick You must bet at least a dollar!
n77=      }
n78=    } 
n79=    else {
n80=      var %bet $readini(%userFile, n, character, bet)
n81=      if (%bet != $null) {
n82=        msg $chan $nick You are betting %bet      
n83=      } 
n84=      else {
n85=        // don't think you can ever get this
n86=        msg $chan $nick You don't have a current bet amount. Use !bet AMOUNT set an amount.
n87=      }
n88=    }
n89=  } 
n90=  elseif ($1 == !playSlots) {
n91=    var %bet $readini(%userFile, n, character, bet)
n92=    var %paid $useMoney(%bet)
n93=
n94=    if (%paid) {
n95=      playSlots
n96=    }
n97=  } 
n98=  elseif ($1 == !cash) {
n99=    var %cash $readini(%userFile, n, character, cash)
n100=    msg $chan $nick has %cash dollars
n101=  } 
n102=}
n103=/*
n104=*************
n105=Aliases
n106=*************
n107=*/
n108=alias -l setVariables {
n109=  // mix of variables from config and manually set
n110=  // if you have this bot on multiple channel, it will fail because the Variable file is vars.ini for both
n111=  // you should either write your variables to a different vars.ini file or change the below variables to 
n112=  // do something like this %gid#myotherchannel
n113=  // that way each channel has its own set of global variables
n114=
n115=  // set config settings  
n116=  Set -e %configFile Slots\Slots.config
n117=
n118=  // read from config file
n119=  Set -e %gameTitle $readini(%configFile, n, config, gameTitle)
n120=  Set -e %welcomeMessage $readini(%configFile, n, config, welcomeMessage)
n121=  Set -e %cashAddAmount $readini(%configFile, n, config, cashAddAmount)
n122=  Set -e %baseDir $readini(%configFile, n, config, baseDir)
n123=  Set -e %userTimer $readini(%configFile, n, config, userTimer)
n124=}
n125=
n126=// creates the user's starting file
n127=alias -l createUserFile {
n128=  var %userFile $+(%baseDir,\users\,$1,.ini)
n129=  writeini %userFile character cash 500
n130=  writeini %userFile character bet 10
n131=}
n132=
n133=alias -l startUserTimer {
n134=  timer $+ $nick 0 %userTimer setUserStats $nick
n135=}
n136=
n137=alias -l setUserStats {
n138=  setStats $1 cash %cashAddAmount
n139=}
n140=
n141=// a function to add whatever stat on the user profile. Used by other functions
n142=// $1 = username, $2 = property to modify, $3 = increment amount
n143=alias -l setStats {
n144=  var %userFile $+(%baseDir,\users\,$1,.ini)
n145=  if ($exists(%userFile)) {
n146=    var %property $readini(%userFile, n, character, $2)
n147=    var %result $calc(%property + $3)
n148=
n149=    if (%result < 0) {
n150=      // shouldn't be any values that go below 0
n151=      var %result 0
n152=    }
n153=
n154=    writeini %userFile character $2 %result
n155=  }
n156=}
n157=
n158=// admin function to add to a users property
n159=// $1 = user $2 = property $3 = amount
n160=alias -l addStats {
n161=  setStats $1 $2 $3
n162=  msg $chan $nick $3 has been added to $2 for $1
n163=} 
n164=
n165=alias -l useMoney {
n166=  if ($1 == $null) {
n167=    // should never get here
n168=    msg ERROR: useMoney - Need to specify money amount
n169=    return $false
n170=  } 
n171=  else {
n172=    var %price $1
n173=
n174=    if (%price isnum) {
n175=      var %userFile $+(%baseDir,\users\,$nick,.ini)
n176=      var %cash $readini(%userFile, n, character, cash)
n177=
n178=      if (%cash < %price) {
n179=        msg $chan $nick You don't have enough money for that.
n180=        return $false
n181=      } 
n182=      else {
n183=        dec %cash %price
n184=        writeini %userFile character cash %cash
n185=        return $true
n186=      }
n187=    } 
n188=    else {
n189=      msg $chan $nick $+(%price, $chr(32), is not a number)
n190=      return $false
n191=    }
n192=  }
n193=}
n194=
n195=
n196=/*
n197=- random chance
n198=- icons: bell, star, horseshoe, bar, cherry
n199=- odds per symbol: 20, 15, 10, 5, 1
n200=- payouts: bet, betx2, betx3, betx5, betx100
n201=*/
n202=
n203=alias -l playSlots {
n204=  var %userFile $+(%baseDir,\users\,$nick,.ini)
n205=  var %bet $readini(%userFile, n, character, bet)
n206=
n207=
n208=  // get 3 random symbols
n209=  %x = 1
n210=  while (%x <= 3) {
n211=    var %random $rand(1,51)
n212=
n213=    if (%random <= 20) {
n214=      var %symbol. $+ %x 2
n215=      var %icon. $+ %x *bell*
n216=    } 
n217=    elseif ((%random > 20) && (%random <= 35)) {
n218=      var %symbol. $+ %x 5
n219=      var %icon. $+ %x *star*
n220=    }
n221=    elseif ((%random > 35) && (%random <= 45)) {
n222=      var %symbol. $+ %x 8
n223=      var %icon. $+ %x *horseshoe*
n224=    }
n225=    elseif ((%random > 45) && (%random <= 50)) {
n226=      var %symbol. $+ %x 10
n227=      var %icon. $+ %x *bar*
n228=    } 
n229=    else {
n230=      // this is the jackpot symbol!
n231=      var %symbol. $+ %x 100
n232=      var %icon. $+ %x *cherry*
n233=    }
n234=
n235=    inc %x
n236=  }
n237=
n238=  var %output $+(%icon.1,-,%icon.2,-,%icon.3,. )
n239=
n240=  // now lets see if we are a winner
n241=  if (( %symbol.1 == %symbol.2) && (%symbol.2 == %symbol.3)) {
n242=    var %winnings $calc(%bet * %symbol.1) 
n243=    if (%symbol.1 == 100) {
n244=      var %output $+(%output, $chr(32), $nick, $chr(32),You win the jackpot!!! Winnings: , %winnings)
n245=    } 
n246=    else {
n247=      var %output $+(%output, $chr(32), $nick, $chr(32), Winner! Winnings: , %winnings)    
n248=    }
n249=
n250=    // payout the winnings
n251=    var %cash $readini(%userFile , n, character, cash)
n252=    inc %cash %winnings
n253=    writeini %userFile character cash %cash
n254=  }
n255=  else {
n256=    var %output $+(%output, $chr(32), $nick, $chr(32), Sorry you didn't win. Try again!)      
n257=  }
n258=
n259=  msg $chan %output
n260=}
